// inspired by https://gist.github.com/Robyer/a6578e60127418b380ca133a1291f017

apply plugin: 'maven-publish'

group 'com.yandex.darkside'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// Because the components are created only during the afterEvaluate phase, you must configure
// your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                version libVersion

                artifact sourceJar

                from components.release

                if (project.hasProperty('teamcity.version')) {
                    pom.description = "Built on commit: ${getGitHash()}"
                }
            }
        }
    }
}
